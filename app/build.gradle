apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def date = new Date()
def formattedDate = date.format('yyyyMMdd_HHmm')

def getDateAsMillis() {
    def Calendar cal = Calendar.getInstance();
    return cal.getTimeInMillis().toString()
}



android {

    def versionPropsFile = file('version.properties')
    def code = 1
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))
        List<String> runTasks = gradle.startParameter.getTaskNames();
        def value = 0
        for (String item : runTasks)
            if ( item.contains("assembleRelease")) {
                value = 1;
            }
        code = Integer.parseInt(versionProps['VERSION_CODE']).intValue() + value
        versionProps['VERSION_CODE']=code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }

    compileSdkVersion 25
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId 'com.avaya.android.vantage.devcala'
        minSdkVersion 23
        //noinspection OldTargetApi
        // 22 and not 23 in order to prevent requesting permissions at run-time
        targetSdkVersion 22
        versionCode 1
        versionName "2.0" + "_" + code + "_" + formattedDate
        multiDexEnabled true
        setProperty("archivesBaseName", "Basic" +"_" +code +"_" + formattedDate )



        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + getDateAsMillis() + "L)"
        buildConfigField "java.lang.String", "CSDK_VERSION", "\"294.0.30\""
        buildConfigField "java.lang.String", "AVAYA_BUILD_NUMBER", "\"0411\""
        buildConfigField "java.lang.String", "USER_AGENT_NAME", "\"Avaya Vantage Basic\""
        buildConfigField "java.lang.String", "AVAYA_VERSION", "\"2.0.0\""
    }

    buildTypes {
        //noinspection GroovyMissingReturnStatement
        release {
            //disable proguard
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility = '1.8'

    }

    lintOptions {
        //checkReleaseBuilds true
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    fileTree(dir: 'libs', include: '**/*.aar')
            .each { File file ->
                dependencies.add("implementation", [name: file.name.lastIndexOf('.').with { it != -1 ? file.name[0..<it] : file.name }, ext: 'aar'])
            }
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:design:25.4.0'
    implementation "com.android.support:cardview-v7:25.4.0"
    implementation "com.android.support:recyclerview-v7:25.4.0"

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'io.netty:netty-all:4.1.0.Beta3'
    implementation 'org.slf4j:slf4j-android:1.7.21'
    implementation 'com.daasuu:BubbleLayout:1.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //implementation 'io.socket:socket.io-client:2.0.3'
    implementation 'com.github.nkzawa:socket.io-client:0.6.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    //Exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.7.1'



    //
    // add these dependencies for the speech client
    // gRPC
    implementation 'javax.annotation:javax.annotation-api:1.2'

    // OAuth2 for Google API
    implementation('com.google.auth:google-auth-library-oauth2-http:0.7.0') {
        exclude module: 'httpclient'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}